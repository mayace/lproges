package com.github.lproges.compiler.es;

import com.github.lproges.compiler.Attr;
import com.github.lproges.compiler.Err;
import com.github.lproges.compiler.Nodo;
import com.github.lproges.compiler.Compiler;
import java.util.LinkedList;
import java.util.ArrayList;
import java.util.Stack;


parser code {: 
    LinkedList<Err> errores = new LinkedList<>();
    LinkedList<Nodo> nodos = new LinkedList<>();

    public LinkedList<Err> getErrores(){
        return errores;
    }
  
    public LinkedList<Nodo> getNodos(){
    	return nodos;
    }

    public void report_error(String message, Object info){
        Err e = new Err(message,null,Err.TIPO.SINTACTICO);

        if(info instanceof java_cup.runtime.Symbol){
            e.setSym((java_cup.runtime.Symbol)info);
        }
        getErrores().add(e);
    }
    public void report_fatal_error(String message,Object info){
        report_error(message,info);
    }
:};

action code{:
	
    void report_error(String msg,Object info){
        parser.report_error(msg,info);
    }
    void addNodo(Nodo nodo){
        parser.getNodos().add(nodo);
    }
    void addError(String msg,Object info,Err.TIPO tipo){
        parser.getErrores().add(new Err(msg,info,tipo));
    }

    Stack<String> scope_stack = new Stack<>();

    String getScope(){
    	String ret = "";
    	int i = 0;
    	for(String str : scope_stack){
    		if (i == 0){
    			ret = str;
    		} else {
    			ret += "." + str;
    		}
    		i++;
    	}
    	System.out.println(ret);
    	return ret;
    }

    void pushScope(String name,Object ... param_list){
    	String scope_str = name;

    	if(param_list != null && param_list.length > 0){
    		for(int i = 0; i<param_list.length; i++){
    			scope_str += (i == 0 ? "," : ".") + param_list[i];
    		}
    	}
    	scope_stack.push(scope_str);
    }

    String popScope(){
    	return scope_stack.pop();
    }
:}


/****************************************************************************************/
terminal        String      KW_INT,KW_STRING,KW_BOOLEAN,KW_FLOAT,KW_CHAR,INT,STRING,BOOLEAN,
                            FLOAT,CHAR,ID,COMA,PCOMA,EQUAL,DEQUAL,LTHAN,BTHAN,LETHAN,BETHAN,
                            NOT,OR,AND,PUNTO,NEQUAL,LP,RP,LB,RB,MINUS,PLUS,MULTI,DIV,PUBLIC,
                            PRIVATE,SELECT,CASE,EXIT,DEFAULT,WHILE,FOR,IF,ELSEIF,KW_NORMAL,
                            KW_INCLUDE,INCLUDE_STRING,INCLUDE_CHAR,KW_OF,KW_ARRAY,CQ,OQ
                            ;
non terminal    Attr        begin,input,stmt_list,stmt,id,int,string,boolean,float,char,def_var,
                            id_list,tipo,expr,include_list,include,def_list,def,main_def,method_body,
                            var_def,dimension,dimension_list,method_def,parm,param_list,
                            param_def,param,method_param
                            ;
/****************************************************************************************/
begin       ::= input 	{:System.err.println("Compiled...");:}
                ;
// *********************************************************************************
// Input - Contenido de la entrada
// *********************************************************************************
input       ::= include_list
				|include_list def_list
				|def_list
                |
                ;
// *********************************************************************************
// Incluir libreriras o archivos...
// *********************************************************************************
include_list	::=	include_list include
					|include
					;
include 		::= KW_INCLUDE INCLUDE_STRING PCOMA
					|KW_INCLUDE INCLUDE_CHAR PCOMA
					;
// *********************************************************************************
// Definiciones alto nivel...
// *********************************************************************************

def_list	::=	def_list def
				|def
				;

def 		::= method_def:x 	
				{:
					addNodo(x.getNodo("nodo"));
				:}
				//|class_def
				|error 		
				;


// *********************************************************************************
// Clase...
// *********************************************************************************

// DEF METODO
method_def	::= tipo:t id:x LP method_param:p RP LB 
												{:
													if(p.getString("tipo_list") == null){
														pushScope(x.getString("val"));
													} else {
														Object[] tipo_array = p.getString("tipo_list").split("\\.");
														pushScope(x.getString("val"),tipo_array);
													}
													
												:} 
												method_body:b RB 
				{:
					
					popScope();
					Attr attr = new Attr();
					Attr l_val = new Attr();

					l_val.set("scope",getScope());
					l_val.set("tipo",t);
					l_val.set("id",x);
					l_val.set("list",p.getList("list"));  
					l_val.set("tipo_list",p.getString("tipo_list"));

					Nodo left = new Nodo(new Attr("val",l_val),false);
					Nodo right = b.getNodo("nodo");

					attr.set("nodo",new Nodo("method",left,right));

					RESULT = attr;

				:}
				;

method_param 	::= param_list:x 	{:RESULT = x;:}
					|				{:RESULT = new Attr("list",new ArrayList<Attr>());:}
					;
param_list 		::= param_list:l COMA param:x
					{:
						l.set("tipo_list",l.getString("tipo_list") + "." + x.getAttr("tipo").getString("val"));
						l.getList("list").add(x);
						RESULT = l;
					:}
					|param:x
					{:
						Attr attr = new Attr();
						ArrayList<Attr> list = new ArrayList<>();
						String tipo_lista = x.getAttr("tipo").getString("val");
						list.add(x);
						attr.set("list",list);
						attr.set("tipo_list",tipo_lista);
						RESULT = attr;
					:}
					;
param 			::= tipo:t id:x
					{:
						Attr attr = new Attr();

						attr.set("tipo",t);
						attr.set("id",x);
						RESULT = attr;
					:}
					;
method_body 	::= stmt_list:x 	{:RESULT = x;:}
				|					{:RESULT = new Attr();:}
				;

// *********************************************************************************
// Sentencias
// *********************************************************************************
stmt_list   ::= stmt_list:l stmt:x
                {:
                	Attr attr = new Attr();

                	attr.set("nodo",new Nodo("stmt",l.getNodo("nodo"),x.getNodo("nodo")));

                	RESULT = attr;
                :}
                |stmt:x
                {:
                	RESULT = x;
                :}
                ;
stmt        ::= var_def:x 	{:RESULT = x;:}
                ;


// *********************************************************************************
// Variables
// *********************************************************************************
// DEFINICIONES
var_def 	::= tipo:t id_list:l PCOMA
				{:
					Attr attr = new Attr();
					Attr l_val = new Attr();

					l_val.set("scope",getScope());
					l_val.set("tipo",t);
					l_val.set("list",l.getList("list"));

					Nodo left = new Nodo(new Attr("val",l_val),false);
					Nodo right = null;

					attr.set("nodo",new Nodo("declaracion",left,right));

					RESULT = attr;
				:}
				|tipo:t id_list:l EQUAL expr:e PCOMA
				{:
					Attr attr = new Attr();
					Attr l_attr = new Attr();

					l_attr.set("tipo",t);
					l_attr.set("list",l.getList("list"));

					Nodo left = new Nodo(new Attr("val",l_attr),false);
					Nodo right = e.getNodo("nodo");

					attr.set("nodo",new Nodo("declaracion",left,right));

					RESULT = attr;
				:}
				|KW_ARRAY KW_OF tipo:t id:x dimension_list:l PCOMA
				{:
					Attr attr = new Attr();
					Attr l_attr = new Attr();

					l_attr.set("scope",getScope());
					l_attr.set("tipo",t);
					l_attr.set("id",x);
					l_attr.set("list",l.get("list"));

					Nodo left = new Nodo(new Attr("val",l_attr),false);

					attr.set("nodo",new Nodo("declaracion_array",left,null));

					RESULT = attr;
				:}
                ;

tipo        ::= KW_CHAR         {:RESULT = new Attr("val","char");:}
                |KW_BOOLEAN     {:RESULT = new Attr("val","boolean");:}
                |KW_STRING      {:RESULT = new Attr("val","string");:}
                |KW_INT         {:RESULT = new Attr("val","int");:}
                |KW_FLOAT       {:RESULT = new Attr("val","float");:}
                |KW_NORMAL 		{:RESULT = new Attr("val","void");:}
                |error          {:RESULT = new Attr();:}
                ;

id_list     ::= id_list:l COMA id:x
				{:
					l.getList("list").add(x);
					RESULT = l;
				:}
                |id:x
                {:
                	Attr attr = new Attr();
                	ArrayList<Attr> list = new ArrayList<>();
                	list.add(x);
                	attr.set("list",list);

                	RESULT = attr;
                :}
                ;

dimension_list 	::=	dimension_list:l dimension:x
					{:
						l.getList("list").add(x);
						RESULT = l;
					:}
					|dimension:x
					{:
						Attr attr = new Attr();
						ArrayList<Attr> list = new ArrayList<>();

						list.add(x);

						attr.set("list",list);

						RESULT = attr;
					:}
					;
dimension 		::= OQ expr:x CQ 	{:RESULT = x;:}
					;

// *********************************************************************************
// Expresion
// *********************************************************************************

expr 		::=	int:x		{:RESULT = x;:}
				|char:x		{:RESULT = x;:}
				|float:x	{:RESULT = x;:}
				|string:x	{:RESULT = x;:}
				|boolean:x	{:RESULT = x;:}
				|id:x 		{:RESULT = x;:}
				;

// *********************************************************************************
// Datos primitivos
// *********************************************************************************
char        ::= CHAR:x   
                {:
                    Attr a = new Attr();
                    a.set("val",x);
                    a.set("tipo","char");
                    a.set("info",CUP$Parser$stack.peek());
                    a.set("nodo",new Nodo(a,false));
                    RESULT = a;
                :}
                ;

float       ::= FLOAT:x   
                {:
                    Attr a = new Attr();
                    a.set("val",x);
                    a.set("tipo","float");
                    a.set("info",CUP$Parser$stack.peek());
                    a.set("nodo",new Nodo(a,false));
                    RESULT = a;
                :}
                ;

boolean     ::= BOOLEAN:x   
                {:
                    Attr a = new Attr();
                    a.set("val",x);
                    a.set("tipo","boolean");
                    a.set("info",CUP$Parser$stack.peek());
                    a.set("nodo",new Nodo(a,false));
                    RESULT = a;
                :}
                ;
string      ::= STRING:x     
                {:
                    Attr a = new Attr();
                    a.set("val",x);
                    a.set("tipo","string");
                    a.set("info",CUP$Parser$stack.peek());
                    a.set("nodo",new Nodo(a,false));
                    RESULT = a;
                :}
                ;
int         ::= INT:x       
                {:
                    Attr a = new Attr();
                    a.set("val",x);
                    a.set("tipo","int");
                    a.set("info",CUP$Parser$stack.peek());
                    a.set("nodo",new Nodo(a,false));
                    RESULT = a;
                :}
                    ;
id          ::= ID:x        
                {:
                    Attr a = new Attr();
                    a.set("val",x);
                    a.set("id",true);
                    a.set("info",CUP$Parser$stack.peek());
                    a.set("nodo",new Nodo(a,true));
                    RESULT = a;
                :}
                ;




