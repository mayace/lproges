package com.github.lproges.compiler.es;

import com.github.lproges.compiler.Attr;
import com.github.lproges.compiler.Err;
import com.github.lproges.compiler.Nodo;
import java.util.LinkedList;
import java.util.ArrayList;


parser code {: 
    LinkedList<Err> errores = new LinkedList<>();
    LinkedList<Nodo> nodos = new LinkedList<>();

    public LinkedList<Err> getErrores(){
        return errores;
    }
  
    public LinkedList<Nodo> getNodos(){
    	return nodos;
    }

    public void report_error(String message, Object info){
        Err e = new Err(message,null,Err.TIPO.SINTACTICO);

        if(info instanceof java_cup.runtime.Symbol){
            e.setSym((java_cup.runtime.Symbol)info);
        }
        getErrores().add(e);
    }
    public void report_fatal_error(String message,Object info){
        report_error(message,info);
    }
:};

action code{:

    void report_error(String msg,Object info){
        parser.report_error(msg,info);
    }
    void addNodo(Nodo nodo){
        parser.getNodos().add(nodo);
    }
    void addError(String msg,Object info,Err.TIPO tipo){
        parser.getErrores().add(new Err(msg,info,tipo));
    }
:}


/****************************************************************************************/
terminal        String      KW_INT,KW_STRING,KW_BOOLEAN,KW_FLOAT,KW_CHAR,INT,STRING,BOOLEAN,
                            FLOAT,CHAR,ID,COMA,PCOMA,EQUAL,DEQUAL,LTHAN,BTHAN,LETHAN,BETHAN,
                            NOT,OR,AND,PUNTO,NEQUAL,LP,RP,LB,RB,MINUS,PLUS,MULTI,DIV,PUBLIC,
                            PRIVATE,SELECT,CASE,EXIT,DEFAULT,WHILE,FOR,IF,ELSEIF,KW_NORMAL,
                            KW_INCLUDE,INCLUDE_STRING,INCLUDE_CHAR,KW_OF,KW_ARRAY,CQ,OQ
                            ;
non terminal    Attr        begin,input,stmt_list,stmt,id,int,string,boolean,float,char,def_var,
                            id_list,tipo,expr,include_list,include,def_list,def,main_def,main_body,
                            var_def,dimension,dimension_list
                            ;
/****************************************************************************************/
begin       ::= input 	{:System.out.println("[Compiler] - Compiled...");:}
                ;
// *********************************************************************************
// Input - Contenido de la entrada
// *********************************************************************************
input       ::= include_list
				|include_list def_list
				|def_list
                |
                ;
// *********************************************************************************
// Incluir libreriras o archivos...
// *********************************************************************************
include_list	::=	include_list include
					|include
					;
include 		::= KW_INCLUDE INCLUDE_STRING PCOMA
					|KW_INCLUDE INCLUDE_CHAR PCOMA
					;
// *********************************************************************************
// Definiciones alto nivel...
// *********************************************************************************

def_list	::=	def_list def
				|def
				;

def 		::= main_def:x 	
				{:
					addNodo(x.getNodo("nodo"));
				:}
				//|class_def
				|error 		{:RESULT = new Attr();:}
				;
// DEFINICION METODO PRINCIPAL
main_def	::=	KW_NORMAL id:x LP RP LB main_body:b RB
				{:
					Attr attr = new Attr();

					attr.set("nodo",new Nodo("main",b.getNodo("nodo"),null));
					
					RESULT = attr;
				:}
				;

main_body 	::= stmt_list:x 	{:RESULT = x;:}
				|				{:RESULT = new Attr();:}
				;

// DEFINICION CLASE

// *********************************************************************************
// Sentencias
// *********************************************************************************
stmt_list   ::= stmt_list:l stmt:x
                {:
                	Attr attr = new Attr();

                	attr.set("nodo",new Nodo("stmt",l.getNodo("nodo"),x.getNodo("nodo")));

                	RESULT = attr;
                :}
                |stmt:x
                {:
                	RESULT = x;
                :}
                ;
stmt        ::= var_def:x 	{:RESULT = x;:}
                ;



// DEFINICION DE VARIABLES
var_def 	::= tipo:t id_list:l PCOMA
				{:
					Attr attr = new Attr();
					Attr l_attr = new Attr();

					l_attr.set("tipo",t);
					l_attr.set("list",l.getList("list"));

					Nodo left = new Nodo(l_attr,false);
					Nodo right = null;

					attr.set("nodo",new Nodo("declaracion",left,right));

					RESULT = attr;
				:}
				|tipo:t id_list:l EQUAL expr:e PCOMA
				{:
					Attr attr = new Attr();
					Attr l_attr = new Attr();

					l_attr.set("tipo",t);
					l_attr.set("list",l.getList("list"));

					Nodo left = new Nodo(l_attr,false);
					Nodo right = e.getNodo("nodo");

					attr.set("nodo",new Nodo("declaracion",left,right));

					RESULT = attr;
				:}
				|KW_ARRAY KW_OF tipo:t id:x dimension_list:l PCOMA
				{:
					Attr attr = new Attr();
					Attr l_attr = new Attr();

					l_attr.set("tipo",t);
					l_attr.set("id",x);
					l_attr.set("dimension",l.get("list"));

					Nodo left = new Nodo(l_attr,false);

					attr.set("nodo",new Nodo("declaracion_array",left,null));

					RESULT = attr;
				:}
                ;

tipo        ::= KW_CHAR         {:RESULT = new Attr("val","char");:}
                |KW_BOOLEAN     {:RESULT = new Attr("val","boolean");:}
                |KW_STRING      {:RESULT = new Attr("val","string");:}
                |KW_INT         {:RESULT = new Attr("val","int");:}
                |KW_FLOAT       {:RESULT = new Attr("val","float");:}
                |error          {:RESULT = new Attr();:}
                ;

id_list     ::= id_list:l COMA id:x
				{:
					l.getList("list").add(x);
					RESULT = l;
				:}
                |id:x
                {:
                	Attr attr = new Attr();
                	ArrayList<Attr> list = new ArrayList<>();
                	list.add(x);
                	attr.set("list",list);

                	RESULT = attr;
                :}
                ;

dimension_list 	::=	dimension_list:l dimension:x
					{:
						l.getList("list").add(x);
						RESULT = x;
					:}
					|dimension:x
					{:
						Attr attr = new Attr();
						ArrayList<Attr> list = new ArrayList<>();

						list.add(x);

						attr.set("list",list);

						RESULT = attr;
					:}
					;
dimension 		::= OQ expr:x CQ 	{:RESULT = x;:}
					;

// *********************************************************************************
// Expresion
// *********************************************************************************

expr 		::=	int:x		{:RESULT = x;:}
				|char:x		{:RESULT = x;:}
				|float:x	{:RESULT = x;:}
				|string:x	{:RESULT = x;:}
				|boolean:x	{:RESULT = x;:}
				|id:x 		{:RESULT = x;:}
				;

// *********************************************************************************
// Datos primitivos
// *********************************************************************************
char        ::= CHAR:x   
                {:
                    Attr a = new Attr();
                    a.set("val",x);
                    a.set("tipo","char");
                    a.set("info",CUP$Parser$stack.peek());
                    RESULT = a;
                :}
                ;

float       ::= FLOAT:x   
                {:
                    Attr a = new Attr();
                    a.set("val",x);
                    a.set("tipo","float");
                    a.set("info",CUP$Parser$stack.peek());
                    RESULT = a;
                :}
                ;

boolean     ::= BOOLEAN:x   
                {:
                    Attr a = new Attr();
                    a.set("val",x);
                    a.set("tipo","boolean");
                    a.set("info",CUP$Parser$stack.peek());
                    RESULT = a;
                :}
                ;
string      ::= STRING:x     
                {:
                    Attr a = new Attr();
                    a.set("val",x);
                    a.set("tipo","string");
                    a.set("info",CUP$Parser$stack.peek());
                    RESULT = a;
                :}
                ;
int         ::= INT:x       
                {:
                    Attr a = new Attr();
                    a.set("val",x);
                    a.set("tipo","int");
                    a.set("info",CUP$Parser$stack.peek());
                    RESULT = a;
                :}
                    ;
id          ::= ID:x        
                {:
                    Attr a = new Attr();
                    a.set("val",x);
                    a.set("id",true);
                    a.set("info",CUP$Parser$stack.peek());
                    RESULT = a;
                :}
                ;




